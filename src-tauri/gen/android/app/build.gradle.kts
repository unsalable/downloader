import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("rust")
}

val tauriProperties = Properties().apply {
    val propFile = file("tauri.properties")
    if (propFile.exists()) {
        propFile.inputStream().use { load(it) }
    }
}

// Release signing. The keystore and its passwords stay outside the repository;
// without them a release build is produced unsigned rather than failing.
val keystoreProperties = Properties().apply {
    val propFile = rootProject.file("keystore.properties")
    if (propFile.exists()) {
        propFile.inputStream().use { load(it) }
    }
}

// -- bundled runtime ----------------------------------------------------------
//
// Python 3, FFmpeg, ffprobe and QuickJS, built for Android and packaged as
// native libraries by the youtubedl-android project. Only the `jni` folders of
// its archives are used; the Kotlin wrapper around them is not, because the
// Rust core starts these programs itself (see src-tauri/src/android.rs).

val bundledRuntimeVersion = "0.18.1"
val bundledRuntimeDir = layout.buildDirectory.dir("bundled-runtime/jniLibs")
val bundledRuntime: Configuration by configurations.creating {
    isTransitive = false
}

val extractBundledRuntime by tasks.registering(Sync::class) {
    from(bundledRuntime.elements.map { files -> files.map { zipTree(it) } }) {
        include("jni/**/*.so")
        eachFile { path = path.removePrefix("jni/") }
        includeEmptyDirs = false
    }
    into(bundledRuntimeDir)
}

tasks.named("preBuild") {
    dependsOn(extractBundledRuntime)
}

android {
    compileSdk = 36
    namespace = "io.universaldownloader.app"
    defaultConfig {
        manifestPlaceholders["usesCleartextTraffic"] = "false"
        applicationId = "io.universaldownloader.app"
        minSdk = 24
        targetSdk = 36
        versionCode = tauriProperties.getProperty("tauri.android.versionCode", "1").toInt()
        versionName = tauriProperties.getProperty("tauri.android.versionName", "1.0")
    }
    signingConfigs {
        if (keystoreProperties.containsKey("storeFile")) {
            create("release") {
                storeFile = file(keystoreProperties.getProperty("storeFile"))
                storePassword = keystoreProperties.getProperty("storePassword")
                keyAlias = keystoreProperties.getProperty("keyAlias")
                keyPassword = keystoreProperties.getProperty("keyPassword")
            }
        }
    }
    buildTypes {
        getByName("debug") {
            manifestPlaceholders["usesCleartextTraffic"] = "true"
            isDebuggable = true
            isJniDebuggable = true
            isMinifyEnabled = false
            packaging {                jniLibs.keepDebugSymbols.add("*/arm64-v8a/*.so")
                jniLibs.keepDebugSymbols.add("*/armeabi-v7a/*.so")
                jniLibs.keepDebugSymbols.add("*/x86/*.so")
                jniLibs.keepDebugSymbols.add("*/x86_64/*.so")
            }
        }
        getByName("release") {
            signingConfigs.findByName("release")?.let { signingConfig = it }
            isMinifyEnabled = true
            proguardFiles(
                *fileTree(".") { include("**/*.pro") }
                    .plus(getDefaultProguardFile("proguard-android-optimize.txt"))
                    .toList().toTypedArray()
            )
        }
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        buildConfig = true
    }
    packaging {
        // Python and FFmpeg are started as programs, which Android only allows
        // from the extracted native library directory. Stored-uncompressed
        // libraries are never extracted, so the legacy packaging is required.
        jniLibs.useLegacyPackaging = true
    }
    sourceSets.getByName("main").jniLibs.srcDir(bundledRuntimeDir)
}

rust {
    rootDirRel = "../../../"
}

dependencies {
    bundledRuntime("io.github.junkfood02.youtubedl-android:library:$bundledRuntimeVersion@aar")
    bundledRuntime("io.github.junkfood02.youtubedl-android:ffmpeg:$bundledRuntimeVersion@aar")
    implementation("androidx.webkit:webkit:1.14.0")
    implementation("androidx.appcompat:appcompat:1.7.1")
    implementation("androidx.activity:activity-ktx:1.10.1")
    implementation("com.google.android.material:material:1.12.0")
    implementation("androidx.lifecycle:lifecycle-process:2.10.0")
    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.1.4")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.0")
}

apply(from = "tauri.build.gradle.kts")